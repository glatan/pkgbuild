# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.44.0'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('ISC')
depends=('openssl' 'zlib')
makedepends=('rust')
checkdepends=('python' 'rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5d53fd6ff8cdafb8e2045f0e4bc4c56fd199edb81838d42ef26c4867b8cf1dfa0f3dbbfb432065dce4921e82f915b230acc98d40053c217338d20b16eca7eb1b')
b2sums=('3fa0d66fb5a1dcb82aa59b59fbdb1ec7cb7a130e0e22bb7fe4fc7b9701cd76965c62a7a930d8ec49ccc87c4c1c747a803376848adf0d968ece23e4bc42acbf67')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
