# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.45.2'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('ISC')
depends=('openssl' 'zlib')
makedepends=('rust')
checkdepends=('python' 'rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('1a1390ffee09d636068c61c57d803a65323e503b65210fa7db531a9a4dc4d81c1b31c9bfcdfe834a60a8e71e51406122bae0acd480a5f740d4b256f20334055a')
b2sums=('583df253c43e414c20265ae1a0e85d766f7586e2e22e3d5ad391cc226f93c0ed518be2f2313ccee9148c33d6ffed06fe93752503852b7cee3ea8980aae3b68d3')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
