# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>
# Contributor: Jguer <joaogg3@gmail.com>

pkgname='yay'
pkgver='10.0.3'
pkgrel='1'
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
url='https://github.com/Jguer/yay'
arch=('x86_64')
license=('GPL3')
depends=('libalpm.so>=12' 'sudo' 'git')
makedepends=('go')
source=("https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3863d9f20bb210181f3adaea147a034312c31ce355f5c212fde1ea0df8d554742f5d8c10dd3027c9b6f5dd50e0151b043ffbccf70e1ab9888e12b605c1d6a0ba')
b2sums=('91cd901d160aa8e9023d758b0579b6accb8b8826552ad187d33193b98579f7e66f521c0d5acfc5cbaf543f7d103616f10317afaff64d77991db4e42b45673b90')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    export EXTRA_GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
    export LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr' install
}
