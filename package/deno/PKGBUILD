# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='deno'
pkgver='1.1.1'
pkgrel='1'
pkgdesc='A secure runtime for JavaScript and TypeScript.'
arch=('x86_64')
url='https://deno.land/'
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'python2' 'rust')
checkdepends=('rust')
source=("git+https://github.com/denoland/${pkgname}.git?#tag=v${pkgver}")
sha512sums=('SKIP')
b2sums=('SKIP')

prepare() {
	cd "${pkgname}" || exit
	git submodule update --init --recursive
	ln -sf /usr/bin/python2 "${srcdir}/python"
	PATH="${srcdir}:${PATH}"
}

build() {
	cd "${pkgname}" || exit
	cargo build -vv --release --locked
}

check() {
	cd "${pkgname}" || exit
	cargo test --bin deno -vv --release --locked
}

package() {
	cd "${pkgname}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
