# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.46.2'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('ISC')
depends=('openssl' 'zlib')
makedepends=('rust')
checkdepends=('python' 'rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('ecccee2de3aacc2b444a994a5a3f3a21b695fd9f77b8c273e36fdb20693514dc741a9997047e3827fe6914f1548c0a6edc4a1625782c753e967f78a14e5bf009')
b2sums=('669ed71945eaedf75e66a1155d2757c255ec3f159047d407febd92ff5d683a7bba2b162c4165d3b6e459bf3dfd00be2d121da30cac427066acd85e6d1a08ac29')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
