# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='wasmtime'
pkgver='0.22.0'
pkgrel='1'
pkgdesc='Standalone JIT-style runtime for WebAssembly, using Cranelift.'
arch=('x86_64')
url='https://wasmtime.dev/'
license=('Apache')
depends=('gcc-libs')
makedepends=('git' 'rustup')
checkdepends=('rust')
source=("git+https://github.com/bytecodealliance/wasmtime.git?#tag=v${pkgver}")
sha512sums=('SKIP')
b2sums=('SKIP')

prepare() {
	cd "${pkgname}" || exit
	rustup default stable
	rustup target add wasm32-wasi # traps::parse_dwarf_info requires wasm32-wasi target
	git submodule update --init --recursive
}

build() {
	cd "${pkgname}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}" || exit
	cargo test --release --locked
}

package() {
	cd "${pkgname}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
}
