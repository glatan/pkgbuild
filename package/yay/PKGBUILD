# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>
# Contributor: Jguer <joaogg3@gmail.com>

pkgname='yay'
pkgver='10.1.0'
pkgrel='1'
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
url='https://github.com/Jguer/yay'
arch=('x86_64')
license=('GPL3')
depends=('libalpm.so>=12' 'sudo' 'git')
makedepends=('go')
source=("https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('836720a769cc142f94aeb8c4f20e1c7096e477a5c05e3bed316fd13f8ddf10a784f53f4d51d478c80b3b16cd947ad91ff03a9198e6ebb99b7e06a37b326aa8e7')
b2sums=('91ea119a297eb1242110d5225d83f6e045687b3009b1cac31f4c79a2bff501a98db84c460071af4f24ebf84d849faa468966fa34a41b7fc12470b186433b2743')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    export EXTRA_GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
    export LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr' install
}
