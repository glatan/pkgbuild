# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.46.0'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('ISC')
depends=('openssl' 'zlib')
makedepends=('rust')
checkdepends=('python' 'rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e5251b772b96e9dc528825fb963951d5abab2022090edd43cce24e789e33faadbcd1470a9a8b945cb7ec6dc455e47594ca4378ae9bf46fc8a27c7578f91134e7')
b2sums=('4dc60f84838c388af3f3ddb22e587b3afeeb75ec72494ec553f29c49784fea04309e03c9371d34674c1aede7b525d5f058b6ddf86f5c7e5ec0dba754bb91eeee')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
