# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>
# Contributor: Jguer <joaogg3@gmail.com>

pkgname='yay'
pkgver='10.0.4'
pkgrel='1'
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
url='https://github.com/Jguer/yay'
arch=('x86_64')
license=('GPL3')
depends=('libalpm.so>=12' 'sudo' 'git')
makedepends=('go')
source=("https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('d474eca1b7462923632577a6345a598355d78061993b3561d8e36c4cf6db3df6c47e09e6a8e9481d6fd206774da3f6bd439fb0e1344ac8baa204a631f21c2a1c')
b2sums=('dbdbdc9bb329920291878835f1d8631cae77e69d5155a28da32afb8214e48521460be36de437d076a7c39bac2f19abd78342172675b88cb22b62bb7068dc8a5e')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    export EXTRA_GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
    export LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr' install
}
