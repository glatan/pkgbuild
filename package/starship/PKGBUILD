# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.45.0'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('ISC')
depends=('openssl' 'zlib')
makedepends=('rust')
checkdepends=('python' 'rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('60243650a8f4958a85083d7a65192596946b9f8abf825db2784a66d36dc0350cf088cd884b9392cc19b26ad6f0634b4f517faac41ce1846d38a6fedd0c65295a')
b2sums=('3fb4af539e4f5fde1412165dc45858fa9e98b3c3284a952314778252e72bd9fa362b90c33bec460999d0edd0e4724a55824d4a7a6cbcc34dd76e7c36237c9e90')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
