# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>
# Contributor: Jguer <joaogg3@gmail.com>

pkgname='yay'
pkgver='10.1.2'
pkgrel='1'
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
url='https://github.com/Jguer/yay'
arch=('x86_64')
license=('GPL3')
depends=('libalpm.so>=12' 'sudo' 'git')
makedepends=('go')
source=("https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('85973a83232398cff94f5dd3f662a2671557b3bc5e45f1cd9d35873fd8f3855194a8e5b8d707b0e644845b1438394250cae5bb6d017bb0b54fca15332ce22047')
b2sums=('bf2de8ae3c7f8507b61f7260a00f2a43e1bc6ee5a1a855ad97b46398c56cabb3dff04fb2fa84f9cb5191bb4b1daa780ab20bdfa11e6889eb6806fe829bb41360')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    export EXTRA_GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
    export LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr' install
}
