# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.41.3'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('custom:ISC')
depends=('openssl' 'zlib')
makedepends=('rust')
checkdepends=('rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('ec0cb5d0b552e64b6306a130e9350073af09b733ce7bc0588fe97644e40b4aad0eff1dbc3c7c02f5c844123d87db75fea9e212ce021380cff3f1bef9e890031c')
b2sums=('03ec135c34e6e7e0debc461e436027069aa03a6d61311ff639218140b6f5fc9c54cff2a8c28cd8274158ffc946e5e22dbccf97c660c5a4a4d895cd7b27528330')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
