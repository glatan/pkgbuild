# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>
# Contributor: Jguer <joaogg3@gmail.com>

pkgname='yay'
pkgver='10.0.0'
pkgrel='1'
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
url='https://github.com/Jguer/yay'
arch=('x86_64')
license=('GPL3')
depends=('libalpm.so>=12' 'sudo' 'git')
makedepends=('go')
source=("https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('194a0fa9fc113636059f4bc94be3057675affb84bf56cb0a2cf3f73e6d93086275cfc4b5a187a3c44d7ecabfeed466c006cb042b65233c393d30bcaf6b447bf0')
b2sums=('52d08dee2f0efc0e6a17f315f4b61df09ad9c9da451ab24bee9a92d731763a42cc22c9e986b36427efb9b43e48cc6e5d08f56bdd14ac9ee76ef7f769547a77d7')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    export EXTRA_GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
    export LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr' install
}
